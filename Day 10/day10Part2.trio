id:@p:ajTest:r:2b388732-47d3cd6b "day10Part2"
name:day10Part2
func
mod:2022-12-23T15:49:28.48Z
src:
  (uri) => do
    
    program: uri.ioReadLines()
      .map(line => do
        parts: line.split()
        {
          instruction: parts[0], 
          operand: if (parts.size == 2) parts[1].parseNumber(),
          clocks: if (parts[0] == "addx") 2 else 1
        }
      end)
      .toGrid()
    
    totalClocks: program.foldCol("clocks", sum) 
  
    x: 1            //accumulator
    ip: 0           //instruction pointer
    ir: program[ip] //instruction register
    
    screen: ""
    
    totalClocks.times(i => do
      
      screen = screen + (if ((x-1..x+1).contains(i.remainder(40))) "#" else ".")
      if ((i-39).remainder(40) == 0) screen = screen + "\n"
      
      ir = ir.set("clocks", ir->clocks -1)
      
      if (ir->clocks == 0) do
        if (ir->instruction == "addx") x = x + ir->operand 
        ip = ip + 1
        if (ip < program.size) ir = program[ip]
      end
      
    end)
    
    screen
      .toGrid
      .addMeta({view:"text"})
    
  end